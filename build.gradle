// Logging
logging.captureStandardOutput LogLevel.WARN
println 'A message which is logged at WARN level'

apply plugin: 'eclipse'
apply plugin: 'idea'
apply from: 'libraries.gradle'

version = '0.0.1-SHAPSHOT'

buildscript {
    apply from: 'libraries.gradle'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath libraries.gradle_versions_plugin
    }
}


apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'


sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    mavenCentral()
}

// https://docs.gradle.org/current/userguide/dependency_management.html#component_selection_rules
allprojects {
    configurations {
        all {
            resolutionStrategy {
                componentSelection {
                    all { ComponentSelection selection ->
                        boolean rejected = ['alpha', 'beta', 'rc'].any { qualifier ->
                            selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                        }
                        if (rejected) {
                            selection.reject('Release candidate')
                        }
                    }
                }
            }
        }

        // Excludes JUnit from all dependencies and from all scopes.
        all*.exclude group: 'junit', module: 'junit'
    }
}

dependencies {
    testCompile libraries.commons_lang3
    testCompile libraries.spring_core
    testCompile libraries.hamcrest_all
    testCompile libraries.testng
}

test {
    // Enables TestNG support (default is JUnit)
    useTestNG()
    
    testLogging {
        showExceptions = true
        showStackTraces = true
        showCauses = true
        exceptionFormat = 'full'
    }

    // Listens to events in the test execution lifecycle
    beforeTest { testDescriptor ->
        logger.lifecycle("Test ${testDescriptor} is running...")
    }

    // Listens to standard out and standard error of the test JVM(s)
    onOutput { testDescriptor, testOutputEvent ->
        logger.lifecycle("Test ${testDescriptor} wrote on ${testOutputEvent.destination}: ${testOutputEvent.message}")
    }

    afterTest { testDescriptor, testResult ->
        logger.lifecycle("Test ${testDescriptor}: result was ${testResult}.")
    }
}


task wrapper(type: Wrapper) {

    // The distribution will be unpacked to the $USER_HOME/.gradle/gradle/dists
    archiveBase = Wrapper.PathBase.GRADLE_USER_HOME
    archivePath = 'gradle/dists'
    distributionUrl = 'https://services.gradle.org/distributions/gradle-2.9-all.zip'
}
